## Import packages
import pandas as pd
import os
import sys

## Flag to indicate rules that will be run locally (e.g. not submitted to slurm)
localrules: copy_config, select_vars, select_SPARE_sample, rename_target_var, split_train_test, copy_SPARE_model, eval_SPARE_scores, sparesvm_test_all, sparesvm_merge_all

###################################
## Default config file 
## Runs the workflow with the test dataset
## Edit this for running it on other datasets
in_config = "../../config/vTest/config_SPARE.yaml"
configfile: f"{in_config}"
###################################

###################################
## Read config vars and lists

dir_input = config["dir_input"]
dir_output = config["dir_output"]
dir_input_covar = dir_input + "/prep_data/out_data"
dir_input_data = dir_input + "/COMBAT/out_data"
rois = config["list_ROIs"]

###################################
## Set out file names

OUT_CONFIG = f"../../{dir_output}/config/config_COMBAT.yaml"

OUT_RESULTS = expand("../../{dir_output}/out_data/combined_{dtype}_{ctype}_COMBAT_SPARE-{stype}.csv", dtype = config["roi_types"], ctype = config["corr_types"], stype = config["spare_types"], dir_output = dir_output) + expand("../../{dir_output}/pred_eval/combined_{dtype}_{ctype}_COMBAT_SPARE-{stype}_eval.csv", dtype = config["roi_types"], ctype = config["corr_types"], stype = config["spare_types"], dir_output = dir_output)

OUT_MODELS = expand("../../{dir_output}/out_models/combined_{dtype}_{ctype}_COMBAT_SPARE-{stype}_Model.pkl.gz", dtype = config["roi_types"], ctype = config["corr_types"], stype = config["spare_types"], dir_output = dir_output)

OUT_FILES = [OUT_CONFIG] + OUT_RESULTS + OUT_MODELS

#OUT_FILES = "../../data/vISTAG1/SPARE/out_data_all/combined_DLMUSE_raw_SPARE-ALL.csv"

#print("Target out files:" + '\n' + '\n'.join(OUT_FILES))
#input()

###################################
## Rules

rule ALL:
    '''
    First rule: lists the final expected out files
    '''
    input: OUT_FILES

rule copy_config:
    '''
    Rule for copying config files to output dir
    '''
    input:
        f"{in_config}"
    output:
        f"{OUT_CONFIG}"
    shell:
        "cp -v {input} {output}"

rule select_vars:
    '''
    Select variables from data
    '''
    input:
        in_csv=f"../../{dir_input_data}/combined_{{dtype}}.csv",
        dict_csv=f"../../config/{rois}"
    params:
        dict_var = 'Code',
        covars ='MRID,Age,Sex,DLICV',
    output:
        f"../../{dir_output}/sel_vars/combined_{{dtype}}.csv"
    resources:
        mem_mb=8000
    shell:
        "python ../../utils/generic/util_select_vars.py {input} {params} {output}"

def get_spare_sample(wildcards):
    path_spare='../../config/' + config["sample_SPARE_" + wildcards.stype]
    return path_spare

rule select_SPARE_sample:
    '''
    Select sample (using list)
    '''
    input:
        sample=get_spare_sample,
        data="../../{dir_output}/sel_vars/combined_{dtype}.csv",
    params:
        key_var = 'MRID'
    output:
        temp("../../{dir_output}/sel_sample/{stype}/combined_{dtype}_all.csv")
    resources:
        mem_mb=8000
    shell:
        "python ../../utils/generic/util_merge_dfs.py {input} {params} {output}"

rule rename_target_var:
    '''
    Rename the name of the second var to target
    '''
    input:
        "../../{dir_output}/sel_sample/{stype}/combined_{dtype}_all.csv"
    params:
        num_col = '1',
        name_col = 'Target'
    output:
        "../../{dir_output}/sel_sample/{stype}/combined_{dtype}_all_renamed.csv"
    resources:
        mem_mb=8000
    shell:
        "python ../../utils/generic/util_rename_column.py {input} {params} {output}"

rule split_train_test:
    '''
    Split spare sample all to train test
    '''
    input:
        "../../{dir_output}/sel_sample/{stype}/combined_{dtype}_all_renamed.csv"
    params:
        strat_vars = 'Sex,Target',
        test_ratio = '0.4'
    output:
        "../../{dir_output}/train_test/{stype}/combined_{dtype}_train.csv",
        "../../{dir_output}/train_test/{stype}/combined_{dtype}_test.csv",
    resources:
        mem_mb=8000
    shell:
        "python ../../utils/generic/util_split_train_test.py {input} {params} {output}"

rule sparesvm_train:
    '''
    Train SPARE model
    '''
    input:
        "../../{dir_output}/train_test/{stype}/combined_{dtype}_train.csv"
    params:
        vkey='MRID',
        target = 'Target'
    output:
        "../../{dir_output}/out_models/combined_{dtype}_SPARE-{stype}_Model.pkl.gz"
    resources:
        mem_mb=16000
    shell:
        "bash ../../utils/spare/util_spare_train.sh {input} {params} {output}"
        
rule sparesvm_test:
    '''
    Test SPARE model using the split test sample
    '''
    input:
        data="../../{dir_output}/train_test/{stype}/combined_{dtype}_test.csv",
        mdl="../../{dir_output}/out_models/combined_{dtype}_SPARE-{stype}_Model.pkl.gz"
    output:
        "../../{dir_output}/pred_test/combined_{dtype}_SPARE-{stype}.csv"
    resources:
        mem_mb=16000
    shell:
        "bash ../../utils/spare/util_spare_test.sh {input} {wildcards.stype} {output}"

rule eval_SPARE_scores:
    '''
    Evaluate accuracy on test sample
    '''
    input:
        data="../../{dir_output}/train_test/{stype}/combined_{dtype}_test.csv",
        pred="../../{dir_output}/pred_test/combined_{dtype}_SPARE-{stype}.csv"
    output:
        "../../{dir_output}/pred_eval/combined_{dtype}_SPARE-{stype}_eval.csv",
    resources:
        mem_mb=8000
    shell:
        "python ../../utils/spare/util_eval_spare.py {input} Target {output}"

rule sparesvm_test_all:
    '''
    Test SPARE model using the split test sample
    '''
    input:
        data="../../{dir_output}/sel_vars/combined_{dtype}.csv",
        mdl="../../{dir_output}/out_models/combined_{dtype}_SPARE-{stype}_Model.pkl.gz"
    output:
        "../../{dir_output}/out_data/combined_{dtype}_SPARE-{stype}.csv"
    resources:
        mem_mb=16000
    shell:
        "bash ../../utils/spare/util_spare_test.sh {input} {wildcards.stype} {output}"
      
def get_spare_results(wildcards):
    data_spare=expand(f"../../{dir_output}/out_data/combined_{{dtype}}_{{ctype}}_COMBAT_SPARE-{{stype}}.csv", dtype = wildcards.dtype, ctype = wildcards.ctype, stype = config["spare_types"])
    return data_spare

rule sparesvm_merge_all:
    '''
    Merge SPARE scores
    '''
    input:
        data=get_spare_results
    output:
        f"../../{dir_output}/out_data_all/combined_{{dtype}}_{{ctype}}_SPARE-ALL.csv"
    resources:
        mem_mb=16000
    shell:
        "python ../../utils/generic/util_merge_dfs_multi.py {output} MRID {input}"
        


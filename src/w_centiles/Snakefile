## Import packages
import pandas as pd

# Default config file
configfile: "./config.yaml"

# Read config vars and lists
in_dir = config["in_dir"]
in_demog = config["in_demog"]
dset_name = config["dset_name"]
out_dir = config["out_dir"]
res_dir = config["res_dir"]
centile_vals = config["centile_vals"]
bin_size = config["bin_size"]
centile_types = config["centile_types"]

# Set output file names
out_files = expand(f"{out_dir}/centiles/{dset_name}_DLMUSE_raw_harmonized_centiles{{ctype}}.csv", ctype = centile_types)
print("Target out files:" + '\n' + '\n'.join(out_files))

# Rules

rule ALL:
    '''
    First rule: lists the final expected out files
    '''
    input: out_files

rule select_rois:
    '''
    Select variables from data
    '''
    input:
        in_csv = in_demog
    params:
        key_col = 'MRID',
        cols_keep = 'MRID,Age',
    output:
        f"{out_dir}/tmp_centiles/sel_vars/{dset_name}_demog.csv"
    resources:
        mem_mb=8000
    shell:
        "python ../../utils/generic/util_select_cols.py {input} {params} {output}"

def get_centiles_sample(wildcards):
    path_centiles = res_dir + '/' + config["sample_centiles_" + wildcards.ctype]
    return path_centiles

rule prepare_sample:
    '''
    Merge demog data to ROIs
    '''
    input:
        sample = get_centiles_sample,
        demog = f"{out_dir}/tmp_centiles/sel_vars/{dset_name}_demog.csv",
        data = f"{in_dir}/{dset_name}_{{dtype}}.csv"
    params:
        key_col = 'MRID'    
    output:
        f"{out_dir}/tmp_centiles/merged/{dset_name}_{{dtype}}_sample{{ctype}}.csv"
    resources:
        mem_mb=8000
    shell:
        "python ../../utils/generic/util_merge_dfs_multi.py {output} {params} {input}"


rule calc_centiles:
    input:
        f"{out_dir}/tmp_centiles/merged/{dset_name}_{{dtype}}_sample{{ctype}}.csv"
    output:
        f"{out_dir}/centiles/{dset_name}_{{dtype}}_centiles{{ctype}}.csv"
    params:
        centile_vals = centile_vals,
        bin_size = bin_size,
    shell:
        "bash ../../utils/centiles/util_calc_centiles.sh {input} {params} {output}"

## Import packages
import pandas as pd
import os
import sys

## Flag to indicate rules that will be run locally (e.g. not submitted to slurm)
localrules: copy_config, copy_covars, merge_data, select_vars, select_COMBAT_sample_train, select_COMBAT_sample_test, copy_COMBAT_model, remove_COMBAT_suffix, calc_derived_ROIs 

## Report data
report: "report/workflow.rst"

## Config file
configfile: "../../config/vTest/config_COMBAT.yaml"

###################################
## Read config vars and lists

dir_input = config["version"] + "/prep_data"
dir_output = config["version"] + "/COMBAT"
rois = config["list_ROIs"]
sample_train = config["sample_COMBAT_train"]
sample_test = config["sample_COMBAT_test"]

###################################
## Set out file names

OUT_CONFIG = expand("../../data/{dir_output}/config/{cname}", cname = [config["config_name"], config["sample_COMBAT_train"], config["sample_COMBAT_test"], config["list_ROIs"]], dir_output = dir_output)

OUT_RESULTS = expand("../../data/{dir_output}/out_data/combined_{dtype}_{ctype}_COMBAT.csv", dtype = config["roi_types"], ctype = config["corr_types"], dir_output = dir_output)

OUT_MODELS = expand("../../data/{dir_output}/out_models/combined_{dtype}_{ctype}_COMBATModel.pkl.gz", dtype = config["roi_types"], ctype = config["corr_types"], dir_output = dir_output)

OUT_FILES = OUT_CONFIG + OUT_RESULTS + OUT_MODELS

print("Target out files:" + '\n' + '\n'.join(OUT_FILES))

###################################
## Rules

rule ALL:
    '''
    First rule: lists the final expected out files
    '''
    input: OUT_FILES

rule copy_config:
    '''
    Rule for copying config files to output dir
    '''
    input:
        "../../config/{cname}"
    output:
        f"../../data/{dir_output}/config/{{cname}}"
    shell:
        "cp -v {input} {output}"

rule merge_data:
    '''
    Merge demog data to ROIs
    '''
    input:
        demog=f"../../data/{dir_input}/out_data/combined_{{dtype}}_{{ctype}}.csv",
        roi=f"../../data/{dir_input}/out_data/combined_{{dtype}}_covars.csv",
    params:
        key_var = 'MRID'
    output:
        temp(f"../../data/{dir_output}/merged/combined_{{dtype}}_{{ctype}}.csv")
    resources:
        mem_mb=8000
    shell:
        "python ../../utils/generic/util_merge_dfs.py {input} {params} {output}"

rule select_vars:
    '''
    Select variables from data
    '''
    input:
        in_csv=f"../../data/{dir_output}/merged/combined_{{dtype}}.csv",
        dict_csv=f"../../config/{rois}"
    params:
        dict_var = 'Code',
        covars ='MRID,Age,Sex,SITE,DLICV',
    output:
        temp(f"../../data/{dir_output}/sel_vars/combined_{{dtype}}.csv")
    resources:
        mem_mb=8000
    shell:
        "python ../../utils/generic/util_select_vars.py {input} {params} {output}"

rule select_COMBAT_sample_train:
    '''
    Select sample (using list)
    '''
    input:
        sample_csv=f"../../config/{sample_train}",
        data_csv=f"../../data/{dir_output}/sel_vars/combined_{{dtype}}.csv"
    params:
        key_var = 'MRID'
    output:
        temp(f"../../data/{dir_output}/sel_sample/combined_{{dtype}}_train.csv")
    resources:
        mem_mb=8000
    shell:
        "python ../../utils/generic/util_merge_dfs.py {input} {params} {output}"

rule select_COMBAT_sample_test:
    '''
    Select sample (using list)
    '''
    input:
        sample_csv=f"../../config/{sample_test}",
        data_csv=f"../../data/{dir_output}/sel_vars/combined_{{dtype}}.csv",
    params:
        key_var = 'MRID'
    output:
        temp(f"../../data/{dir_output}/sel_sample/combined_{{dtype}}_test.csv")
    resources:
        mem_mb=8000
    shell:
        "python ../../utils/generic/util_merge_dfs.py {input} {params} {output}"

rule filter_age_COMBAT_sample_test:
    '''
    Select sample (using list)
    '''
    input:
        f"../../data/{dir_output}/sel_sample/combined_{{dtype}}_test.csv"
    params:
        var_name = 'Age',
        min_val = '20',
        max_val = '97'
    output:
        temp(f"../../data/{dir_output}/sel_sample/combined_{{dtype}}_test_filtered.csv")
    resources:
        mem_mb=8000
    shell:
        "python ../../utils/generic/util_filter_var.py {input} {params} {output}"

rule COMBAT_train:
    '''
    Train COMBAT model
    '''    
    input:
        f"../../data/{dir_output}/sel_sample/combined_{{dtype}}_train.csv"
    params:
        batch='SITE',
        vkey='MRID',
        vnum='DLICV',
        vcat='Sex',
        vspline='Age'
    output:
        mdl=f"../../data/{dir_output}/out_models/combined_{{dtype}}_COMBATModel.pkl.gz",
        csv=temp(f"../../data/{dir_output}/out_models/combined_{{dtype}}_train_COMBATOut.csv")
    resources:
        mem_mb=16000
    shell:
        "bash ../../utils/combat/util_combat_train.sh {input} {params} {output}"

rule COMBAT_apply:
    '''
    Apply COMBAT model to calculate harmonized values
    '''    
    input:
        data=f"../../data/{dir_output}/sel_sample/combined_{{dtype}}_test_filtered.csv",
        mdl=f"../../data/{dir_output}/out_models/combined_{{dtype}}_COMBATModel.pkl.gz"
    output:
        temp(f"../../data/{dir_output}/harmonized/combined_{{dtype}}_COMBAT_init.csv")
    resources:
        mem_mb=16000
    shell:
        "echo neuroharm -a apply -i {input.data} -m {input.mdl} -u {output};"
        "neuroharm -a apply -i {input.data} -m {input.mdl} -u {output}"

rule remove_COMBAT_suffix:
    '''
    Remove COMBAT suffix (_HARM)
    FIXME: Ideally COMBAT should be run with an arg to create output var names without a suffix
    '''        
    input:
        f"../../data/{dir_output}/harmonized/combined_{{dtype}}_COMBAT_init.csv"
    params:
        rm_suff='_HARM'
    output:
        temp(f"../../data/{dir_output}/harmonized/combined_{{dtype}}_COMBAT_nosuffix.csv")
    resources:
        mem_mb=8000
    shell:
        "python ../../utils/generic/util_remove_suffix.py {input} {params} {output}; "
        
rule calc_derived_ROIs:
    '''
    Calculate derived ROIs from harmonized data
    - If the input data already includes derived ROIs, they will not be updated
    '''        
    input:
        in_csv=f"../../data/{dir_output}/harmonized/combined_{{dtype}}_COMBAT_nosuffix.csv",
        dict=f"../../config/{rois}"
    params:
        key_var='MRID',
        roi_prefix='MUSE_'
    output:
        csv=f"../../data/{dir_output}/out_data/combined_{{dtype}}_COMBAT.csv"
    resources:
        mem_mb=8000
    shell:
        "python ../../utils/generic/util_combine_MUSE_rois.py {input} {params} {output}"

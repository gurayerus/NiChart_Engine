## Import packages
import pandas as pd
import os
import sys

## Flag to indicate rules that will be run locally (e.g. not submitted to slurm)
localrules: copy_config, select_vars, select_SPARE_sample, rename_target_var, split_train_test, copy_SPARE_model, eval_SPARE_scores

## Config file
configfile: "../../config/vTest/config_SPARE.yaml"

###################################
## Read config vars and lists

dir_input_covar = config["version"] + "/prep_data/out_data"
dir_input_data = config["version"] + "/COMBAT/out_data"
dir_output = config["version"] + "/SPARE"
rois = config["list_ROIs"]

###################################
## Set out file names

OUT_CONFIG = expand("../../data/{dir_output}/config/{cname}", cname = [config["config_name"], config["sample_SPARE_AD"], config["sample_SPARE_Age"], config["list_ROIs"]], dir_output = dir_output)

OUT_RESULTS = expand("../../data/{dir_output}/out_data/combined_{dtype}_{ctype}_COMBAT_SPARE-{stype}.csv", dtype = config["roi_types"], ctype = config["corr_types"], stype = config["spare_types"], dir_output = dir_output)

OUT_MODELS = expand("../../data/{dir_output}/out_models/combined_{dtype}_{ctype}_COMBAT_SPARE-{stype}_Model.pkl.gz", dtype = config["roi_types"], ctype = config["corr_types"], stype = config["spare_types"], dir_output = dir_output)

OUT_FILES = OUT_CONFIG + OUT_RESULTS + OUT_MODELS

print("Target out files:" + '\n' + '\n'.join(OUT_FILES))
#input()

###################################
## Rules

rule ALL:
    '''
    First rule: lists the final expected out files
    '''
    input: OUT_FILES

rule copy_config:
    '''
    Rule for copying config files to output dir
    '''
    input:
        "../../config/{cname}"
    output:
        f"../../data/{dir_output}/config/{{cname}}"
    shell:
        "cp -v {input} {output}"

rule select_vars:
    '''
    Select variables from data
    '''
    input:
        in_csv=f"../../data/{dir_input_data}/combined_{{dtype}}.csv",
        dict_csv=f"../../config/{rois}"
    params:
        dict_var = 'Code',
        covars ='MRID,Age,Sex,DLICV',
    output:
        f"../../data/{dir_output}/sel_vars/combined_{{dtype}}.csv"
    resources:
        mem_mb=8000
    shell:
        "python ../../utils/generic/util_select_vars.py {input} {params} {output}"

def get_spare_sample(wildcards):
    path_spare='../../config/' + config["sample_SPARE_" + wildcards.stype]
    return path_spare

rule select_SPARE_sample:
    '''
    Select sample (using list)
    '''
    input:
        sample=get_spare_sample,
        data="../../data/{dir_output}/sel_vars/combined_{dtype}.csv",
    params:
        key_var = 'MRID'
    output:
        "../../data/{dir_output}/sel_sample/{stype}/combined_{dtype}_all.csv"
    resources:
        mem_mb=8000
    shell:
        "python ../../utils/generic/util_merge_dfs.py {input} {params} {output}"

rule rename_target_var:
    '''
    Rename the name of the second var to target
    '''
    input:
        "../../data/{dir_output}/sel_sample/{stype}/combined_{dtype}_all.csv"
    params:
        num_col = '1',
        name_col = 'Target'
    output:
        "../../data/{dir_output}/sel_sample/{stype}/combined_{dtype}_all_renamed.csv"
    resources:
        mem_mb=8000
    shell:
        "python ../../utils/generic/util_rename_column.py {input} {params} {output}"

rule split_train_test:
    '''
    Split spare sample all to train test
    '''
    input:
        "../../data/{dir_output}/sel_sample/{stype}/combined_{dtype}_all_renamed.csv"
    params:
        strat_vars = 'Sex,Target',
        test_ratio = '0.4'
    output:
        "../../data/{dir_output}/train_test/{stype}/combined_{dtype}_train.csv",
        "../../data/{dir_output}/train_test/{stype}/combined_{dtype}_test.csv",
    resources:
        mem_mb=8000
    shell:
        "python ../../utils/generic/util_split_train_test.py {input} {params} {output}"

rule sparesvm_train:
    '''
    Train SPARE model
    '''
    input:
        "../../data/{dir_output}/train_test/{stype}/combined_{dtype}_train.csv"
    params:
        vkey='MRID',
        target = 'Target'
    output:
        "../../data/{dir_output}/out_models/combined_{dtype}_SPARE-{stype}_Model.pkl.gz"
    resources:
        mem_mb=16000
    shell:
        "bash ../../utils/spare/util_spare_train.sh {input} {params} {output}"
        
rule sparesvm_test:
    '''
    Test SPARE model using the split test sample
    '''
    input:
        data="../../data/{dir_output}/train_test/{stype}/combined_{dtype}_test.csv",
        mdl="../../data/{dir_output}/out_models/combined_{dtype}_SPARE-{stype}_Model.pkl.gz"
    output:
        "../../data/{dir_output}/out_data/combined_{dtype}_SPARE-{stype}.csv"
    resources:
        mem_mb=16000
    shell:
        "bash ../../utils/spare/util_spare_test.sh {input} {output}"

rule eval_SPARE_scores:
    '''
    Evaluate accuracy on test sample
    '''
    input:
        data="../../data/{dir_output}/train_test/{stype}/combined_{dtype}_test.csv",
        pred="../../data/{dir_output}/spare_scores/combined_{dtype}_SPARE-{stype}.csv"
    output:
        "../../data/{dir_output}/spare_scores/combined_{dtype}_SPARE-{stype}_eval.csv",
    resources:
        mem_mb=8000
    shell:
        "python ../../utils/spare/util_eval_spare.py {input} {output}"


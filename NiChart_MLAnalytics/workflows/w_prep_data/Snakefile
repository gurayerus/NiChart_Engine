## Import packages
import pandas as pd

## Flag to indicate rules that will be run locally (e.g. not submitted to slurm)
##localrules: rename_muse_rois
    
## Report data
report: "report/workflow.rst"

## Config file
configfile: "../../config/config.yaml"

## Read study list from config
df = pd.read_csv(config["list_studies"])
LIST_STUDIES = df.Study.tolist()

###################################
## Rules

OUT_FILES = expand("../../data/data_prep/NiChart_{dtype}.csv", dtype = ['MUSE', 'DLMUSE'])

rule ALL:
    '''
    First rule: lists the final expected out files
    '''
    input: OUT_FILES
    
rule rename_rois:
    '''
    Rename MUSE roi indices to roi codes
    '''
    input:
        roi="../../data/init_data_istaging/{dtype}/{study}_{dtype}.csv",
        dict="../../config/list_MUSE_allROI.csv"
    output:
        temp("../../data/data_prep/renamed/{study}_{dtype}.csv")
    params:
        var_from = 'Index',
        var_to = 'Code'
    resources:
        mem_mb=8000
    shell:
        "python ../../utils/util_rename_df_columns.py {input} {params} {output}"

rule merge_data:
    '''
    Merge demog data to ROIs
    '''
    input:
        demog="../../data/init_data_istaging/DemogClin/{study}_DemogClin.csv",
        roi="../../data/data_prep/renamed/{study}_{dtype}.csv"
    params:
        key_var = 'MRID'
    output:
        temp("../../data/data_prep/merged/{study}_{dtype}.csv")
    resources:
        mem_mb=8000
    shell:
        "python ../../utils/util_merge_dfs.py {input} {params} {output}"

rule select_vars:
    '''
    Select variables from data
    '''
    input:
        in_csv="../../data/data_prep/merged/{study}_{dtype}.csv",
        dict_csv="../../config/list_MUSE_allROI.csv"
    params:
        dict_var = 'Code',
        vars_list = 'MRID,Age,Sex,DLICV,Study,PTID',
    output:
        temp("../../data/data_prep/sel_vars/{study}_{dtype}.csv")
    resources:
        mem_mb=8000
    shell:
        "python ../../utils/util_select_vars.py {input} {params} {output}"

rule concat_studies:
    '''
    Combine studies into a single csv
    '''
    input:
        expand("../../data/data_prep/sel_vars/{study}_{{dtype}}.csv", study=LIST_STUDIES)
    params:
        key_var = 'MRID'
    output:
        "../../data/data_prep/NiChart_{dtype}.csv"
    resources:
        mem_mb=8000
    shell:
        "python ../../utils/util_concat_dfs.py {output} {input}"

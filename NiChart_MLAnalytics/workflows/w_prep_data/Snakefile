## Import packages
import pandas as pd

## Flag to indicate rules that will be run locally (e.g. not submitted to slurm)
localrules: copy_config, make_sample_list, select_sample, rename_rois, merge_data, rename_rois_subsampled, merge_data_subsampled, concat_studies, select_covars, select_roi_vars, merge_covars_to_rois, correct_icv, merge_covars_to_corr_rois
    
### Report data
#report: "report/workflow.rst"

## Config file
configfile: "../../config/config_vISTAG_prep_data.yaml"

## Read study list from config
df = pd.read_csv("../../config/" + config["list_studies_merge"])
LIST_STUDIES = df.Study.tolist()

###################################
## Out file names

OUT_CONFIG = expand("../../data/{dir_output}/config/{cname}", cname = [config["config_name"],  config["list_covars"], config["list_ROIs"], config["list_studies_merge"]], dir_output = config["dir_output"])

OUT_RESULTS = expand("../../data/{dir_output}/out_data/combined_{dtype}_{ctype}.csv", dtype = config["roi_types"], ctype = ['covars'] + config["corr_types"], dir_output = config["dir_output"])

OUT_FILES = OUT_CONFIG + OUT_RESULTS

print("Target out files:" + '\n' + '\n'.join(OUT_FILES) + '\n')

###################################
## Rules

rule ALL:
    '''
    First rule: lists the final expected out files
    '''
    input: OUT_FILES

rule copy_config:
    '''
    Rule for copying config files to output dir
    '''
    input:
        "../../config/{cname}"
    output:
        expand("../../data/{dir_output}/config/{{cname}}", dir_output = config["dir_output"])
    shell:
        "cp -v {input} {output}"
    
rule make_sample_list:
    '''
    Rule for selecting a smaller subset of a study dataset
    '''
    input:
        expand("../../data/{dir_input}/Studies/{{study}}/{{study}}_DLMUSE.csv", dir_input = config["dir_input"]),
    output:
        "../../data/{dir_output}/subsampled/{study}-n{num_sample}/sample_{study}-n{num_sample}.csv"
    params:
        random_state = '42',
        out_vars = 'MRID',
    shell:
        "python ../../utils/util_subsample_df.py {input} {wildcards.num_sample} {params} {output}"

rule select_sample:
    '''
    Rule for creating data files with the selected sample
    '''
    input:
        in_csv=expand("../../data/{dir_input}/Studies/{{study}}/{{study}}_{{dtype}}.csv", dir_input = config["dir_input"]),
        in_list="../../data/{dir_output}/subsampled/{study}-n{num_sample}/sample_{study}-n{num_sample}.csv"
    output:
        temp("../../data/{dir_output}/subsampled/{study}-n{num_sample}/{study}-n{num_sample}_{dtype}.csv")
    params:
        key_var = 'MRID'
    shell:
        "python ../../utils/util_merge_dfs.py {input} {params} {output}"
    
rule rename_rois:
    '''
    Rename MUSE roi indices to roi codes
    '''
    input:
        roi=expand("../../data/{dir_input}/Studies/{{study}}/{{study}}_{{dtype}}.csv", dir_input = config["dir_input"]),
        dict=expand("../../config/{rois}", rois = config["list_ROIs"])
    output:
        temp("../../data/{dir_output}/renamed/{study}_{dtype}.csv")
    params:
        var_from = 'Index',
        var_to = 'Code'
    resources:
        mem_mb=8000
    shell:
        "python ../../utils/util_rename_df_columns.py {input} {params} {output}"

rule merge_data:
    '''
    Merge demog data to ROIs
    '''
    input:
        demog=expand("../../data/{dir_input}/Studies/{{study}}/{{study}}_DemogClin.csv", dir_input = config["dir_input"]),
        roi="../../data/{dir_output}/renamed/{study}_{dtype}.csv"
    params:
        key_var = 'MRID'
    output:
        temp("../../data/{dir_output}/merged/{study}_{dtype}.csv")
    resources:
        mem_mb=8000
    shell:
        "python ../../utils/util_merge_dfs.py {input} {params} {output}"

rule rename_rois_subsampled:
    '''
    Rename MUSE roi indices to roi codes for subsampled studies
    '''
    input:
        roi="../../data/{dir_output}/subsampled/{study}/{study}_{dtype}.csv",
        dict=expand("../../config/{rois}", rois = config["list_ROIs"])
    output:
        temp("../../data/{dir_output}/renamed/{study}_{dtype}.csv")
    params:
        var_from = 'Index',
        var_to = 'Code'
    resources:
        mem_mb=8000
    shell:
        "python ../../utils/util_rename_df_columns.py {input} {params} {output}"

rule merge_data_subsampled:
    '''
    Merge demog data to ROIs for subsampled studies
    '''
    input:
        demog="../../data/{dir_output}/subsampled/{study}/{study}_DemogClin.csv",
        roi="../../data/{dir_output}/renamed/{study}_{dtype}.csv"
    params:
        key_var = 'MRID'
    output:
        temp("../../data/{dir_output}/merged/{study}_{dtype}.csv")
    resources:
        mem_mb=8000
    shell:
        "python ../../utils/util_merge_dfs.py {input} {params} {output}"

rule concat_studies:
    '''
    Combine studies into a single csv
    '''
    input:
        expand("../../data/{{dir_output}}/merged/{study}_{{dtype}}.csv", study=LIST_STUDIES)
    params:
        key_var = 'MRID'
    output:
        temp("../../data/{dir_output}/combined/combined_{dtype}.csv")
    resources:
        mem_mb=8000
    shell:
        "python ../../utils/util_concat_dfs.py {output} {input}"

rule qc_ROIs:
    '''
    Discard QC fail cases based on distributions
    '''
    input:
        "../../data/{dir_output}/combined/combined_{dtype}.csv"
    output:
        temp("../../data/{dir_output}/qced/combined_{dtype}.csv")
    resources:
        mem_mb=8000
    shell:
        "python ../../utils/util_qc_ROIs.py {input} {output}"


ruleorder: select_covars > correct_icv

rule select_covars:
    '''
    Select variables from data
    '''
    input:
        in_csv="../../data/{dir_output}/qced/combined_{dtype}.csv",
        dict_csv=expand("../../config/{covars}", covars = config["list_covars"])        
    params:
        dict_var = 'Name',
        vars_list = 'MRID',
    output:
        temp("../../data/{dir_output}/split_tables/combined_{dtype}_covars.csv")
    resources:
        mem_mb=8000
    shell:
        "python ../../utils/util_select_vars.py {input} {params} {output}"

rule select_roi_vars:
    '''
    Select variables from data
    '''
    input:
        in_csv="../../data/{dir_output}/qced/combined_{dtype}.csv",
        dict=expand("../../config/{rois}", rois = config["list_ROIs"])
    params:
        dict_var = 'Code',
        vars_list = 'MRID',
    output:
        temp("../../data/{dir_output}/split_tables/combined_{dtype}_initROIs.csv")
    resources:
        mem_mb=8000
    shell:
        "python ../../utils/util_select_vars.py {input} {params} {output}"

rule correct_icv:
    '''
    Normalize ROIs. Values are scaled either by a constant factor (NormICV) or 100 (PercICV)
    '''
    input:
        "../../data/{dir_output}/split_tables/combined_{dtype}_initROIs.csv"
    params:
        icv_var = 'MUSE_702',
        exclude_vars = 'MRID',
        suffix = 'NONE'
    output:
        temp("../../data/{dir_output}/split_tables/combined_{dtype}_{corr_type}.csv")
    resources:
        mem_mb=8000
    shell:
        "python ../../utils/util_corr_icv.py {input} {wildcards.corr_type} {params} {output}"

rule drop_na:
    '''
    Drop NAs in final data
    '''
    input:
        "../../data/{dir_output}/split_tables/combined_{dtype}.csv"
    output:
        "../../data/{dir_output}/out_data/combined_{dtype}.csv"
    resources:
        mem_mb=8000
    shell:
        "python ../../utils/util_drop_na.py {input} {output}"

## Workflow for selecting a smaller sample for large studies (e.g. UKBIOBANK)

import pandas as pd
import os

## Create a report for the workflow
report: "report/workflow.rst"

## Read params from the config file
configfile: "../../config/config.yaml"

df_tmp = pd.read_csv(config["subsample_list_study"])
LIST_STUDY = df_tmp.Study.tolist()
LIST_NSAMPLE = df_tmp.NumSample.tolist()

LIST_DTYPE = ['DemogClin', 'MUSE', 'DLMUSE']

OUT_FILES = expand("../../data/sample_studies/{study}-n{nsample}/{study}-n{nsample}_DemogClin.csv", zip, study = LIST_STUDY, nsample = LIST_NSAMPLE), expand("../../data/sample_studies/{study}-n{nsample}/{study}-n{nsample}_MUSE.csv", zip, study = LIST_STUDY, nsample = LIST_NSAMPLE), expand("../../data/sample_studies/{study}-n{nsample}/{study}-n{nsample}_DLMUSE.csv", zip, study = LIST_STUDY, nsample = LIST_NSAMPLE)

###################################
## Rules
rule ALL:
    '''
    First rule: lists the final expected out files
    '''
    input: OUT_FILES

rule select_sample:
    '''
    Rule for selecting a smaller subset of a study dataset
    '''
    input:
        "../../data/init_data_istaging/Studies/{study}/{study}_DLMUSE.csv",
    output:
        temp("../../data/sample_studies/{study}-n{num_sample}/sample_{study}-n{num_sample}.csv")
    params:
        out_vars = 'MRID',
    shell:
        "python ../../utils/util_subsample_df.py {input} {wildcards.num_sample} {params} {output}"

rule filter_data:
    '''
    Rule for creating data files with the selected sample
    '''
    input:
        in_csv="../../data/init_data_istaging/Studies/{study}/{study}_{dtype}.csv",
        in_list="../../data/sample_studies/{study}-n{num_sample}/sample_{study}-n{num_sample}.csv"
    output:
        "../../data/sample_studies/{study}-n{num_sample}/{study}-n{num_sample}_{dtype}.csv"
    params:
        key_var = 'MRID'
    shell:
        "python ../../utils/util_merge_dfs.py {input} {params} {output}"
